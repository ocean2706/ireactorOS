/*
 * This file is part of the libpayload project.
 *
 * Copyright (C) 2014 Imagination Technologies
 *
 * Based on src/arch/arm/ramstage.ld:
 *   Written by Johan Rydberg, based on work by Daniel Kahlin.
 *   Rewritten by Eric Biederman
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License as
 * published by the Free Software Foundation; version 2 of
 * the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc.
 */

OUTPUT_ARCH(mips)

BASE_ADDRESS = 0x00000000;
ENTRY(_entry)

HEAP_SIZE = 2*64*1024;
STACK_SIZE = 16*1024;

SECTIONS
{
	. = BASE_ADDRESS;

	. = ALIGN(16);
	_start = .;

	.text : {
		*(.text._entry)
		*(.text)
		*(.text.*)
	}

	.rodata : {
		*(.rodata)
		*(.rodata.*)
	}

	.data : {
		*(.data)
		*(.data.*)
	}

	_edata = .;

	.sdata : {
		*(.srodata)
		*(.sdata)
	}

	_bss = .;
	.bss : {
		*(.sbss)
		*(.sbss.*)
		*(.bss)
		*(.bss.*)
		*(COMMON)

		/* Stack and heap */

		. = ALIGN(16);
		_heap = .;
		. += HEAP_SIZE;
		. = ALIGN(16);
		_eheap = .;

		_estack = .;
		. += STACK_SIZE;
		. = ALIGN(16);
		_stack = .;
	}
	_ebss = .;

	_end = .;

	/DISCARD/ : {
		*(.comment)
		*(.note*)
		*(.reginfo)

	}
}
